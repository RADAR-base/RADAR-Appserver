plugins {
    id 'pmd'
    id "com.github.lkishalmi.gatling" version "3.3.4"
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'org.springframework.boot' version "2.6.6"
    id "org.jetbrains.kotlin.plugin.spring" version "1.6.20"
    id "org.jetbrains.kotlin.plugin.jpa" version "1.6.20"
    id "org.jetbrains.kotlin.jvm" version "1.6.20"
    id "org.jetbrains.kotlin.kapt" version "1.6.20"
}

apply plugin: 'checkstyle'
apply plugin: 'kotlin'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'scala'

group = 'org.radarbase'
version = '1.4.0-SNAPSHOT'
sourceCompatibility = 11

repositories {
    mavenCentral()
}

bootJar {
    mainClass = 'org.radarbase.appserver.AppserverApplication'
}

//apply from: 'gradle/liquibase.gradle'

ext {
    springBootVersion = '2.6.6'
    springOauth2Version = "2.5.1.RELEASE"
    springDocVersion = '1.6.6'
    lombokVersion = '1.18.22'
    bootAdminVersion = '2.6.4'
    throwingFunctionVersion = '1.5.1'
    junit5Version = '5.8.2'
    radarSpringAuthVersion = '1.1.4'
}

sourceSets {
    main.kotlin.srcDirs += 'src/main/java'

    integrationTest {
        java {
            compileClasspath += main.output + test.output + test.compileClasspath
            runtimeClasspath += main.output + test.output + test.runtimeClasspath
            srcDir file('src/integrationTest/java')
        }
        resources.srcDir file('src/integrationTest/resources')
    }
}

kapt {
    keepJavacAnnotationProcessors = true
}

allOpen {
    annotation("javax.persistence.Entity")
    annotation("javax.persistence.Embeddable")
    annotation("javax.persistence.MappedSuperclass")
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')
    implementation('org.springframework.boot:spring-boot-starter-data-rest')
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-quartz')
    implementation('org.springframework.boot:spring-boot-starter-security')
    implementation('org.springframework.boot:spring-boot-starter-actuator')
    implementation('org.springframework.security.oauth.boot:spring-security-oauth2-autoconfigure:' + springBootVersion)
    implementation('org.springframework.security.oauth:spring-security-oauth2:' + springOauth2Version)

    implementation("com.fasterxml.jackson.module:jackson-module-kotlin")
    implementation("org.jetbrains.kotlin:kotlin-reflect")
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")

    kapt("org.springframework.boot:spring-boot-configuration-processor")
    kapt("org.springframework:spring-context-indexer:5.3.18")
    kapt("org.projectlombok:lombok:" + lombokVersion)

    implementation group: 'com.pivovarit', name: 'throwing-function', version: throwingFunctionVersion

    implementation(group: 'de.codecentric', name: 'spring-boot-admin-starter-client', version: bootAdminVersion)

    // Open API spec
    implementation(group: 'org.springdoc', name: 'springdoc-openapi-ui', version: springDocVersion)

    implementation 'org.scala-lang:scala-library:2.13.8'

    //runtimeOnly('org.springframework.boot:spring-boot-devtools')
    runtimeOnly('org.hsqldb:hsqldb')
    runtimeOnly('org.liquibase:liquibase-core')
    runtimeOnly('com.h2database:h2:1.4.200')
    runtimeOnly(group: 'org.postgresql', name: 'postgresql', version: '42.3.3')


    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    implementation group: 'org.projectlombok', name: 'lombok', version: lombokVersion

    annotationProcessor 'org.springframework:spring-context-indexer:5.3.18'

    compileOnly 'net.jcip:jcip-annotations:1.0'

    // FCM Admin SDK
    implementation 'com.google.firebase:firebase-admin:8.1.0'

    // AOP
    runtimeOnly group: 'org.springframework', name: 'spring-aop', version: '5.3.18'
    implementation(group: 'org.radarbase', name: 'radar-spring-auth', version: radarSpringAuthVersion) {
        exclude group: 'javax.servlet', module: 'servlet-api'
    }

    testImplementation group: 'io.gatling.highcharts', name: 'gatling-charts-highcharts', version: '3.6.1'

    implementation('org.liquibase.ext:liquibase-hibernate5:4.9.0')

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit', module: 'junit'
        //exclude group: 'org.mockito', module: 'mockito-core'
    }
    //testImplementation("com.ninja-squad:springmockk:3.1.1")

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: junit5Version
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junit5Version
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junit5Version
    testImplementation group: 'org.junit.platform', name: 'junit-platform-commons', version: '1.8.2'
    testImplementation group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.8.2'
    testImplementation group: 'org.junit.platform', name: 'junit-platform-engine', version: '1.8.2'

    gatling('com.fasterxml.jackson.datatype:jackson-datatype-jsr310')

}

checkstyle {
    configDirectory = file("config/checkstyle")
    toolVersion = "8.20"
    showViolations = false
    sourceSets = [it.sourceSets.main]
}

pmd {
    toolVersion = "6.44.0"
}

javadoc {
    options.addBooleanOption('html5', true)
    destinationDir = new File("${project.rootDir}/src/main/resources/static/java-docs".toString())
}

wrapper {
    gradleVersion '7.4'
}

test {
    useJUnitPlatform() {
        excludeEngines 'junit-vintage'
    }
}

task unpack(type: Copy) {
    duplicatesStrategy = 'include'
    dependsOn bootJar
    from(zipTree(tasks.bootJar.outputs.files.singleFile))
    into("build/dependency")
}

task loadTest(type: JavaExec) {
    dependsOn testClasses
    description = "Load Test With Gatling"
    group = "Load Test"
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs = [
            "-Dgatling.core.directory.binaries=${sourceSets.main.output.classesDirs.toString()}"
    ]
    mainClass = "io.gatling.app.Gatling"
    args = [
            "--simulation", "org.radarbase.appserver.ApiGatlingSimulationTest",
            "--results-folder", "${buildDir}/gatling-results",
            "--binaries-folder", sourceSets.main.output.classesDirs.toString(),
            "--bodies-folder", sourceSets.main.resources.srcDirs.toList().first().toString() + "/gatling/bodies",
    ]
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    shouldRunAfter test
}

rootProject.tasks.named("processIntegrationTestResources") {
    duplicatesStrategy = 'include'
}

check.dependsOn integrationTest

integrationTest {
    useJUnitPlatform() {
        excludeEngines 'junit-vintage'
    }
    environment "RADAR_IS_CONFIG_LOCATION", "src/integrationTest/resources/radar_is.yml"
}

test {
    testLogging {
        events "failed"
        exceptionFormat "full"

        error {
            exceptionFormat "full"
        }
    }
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}
