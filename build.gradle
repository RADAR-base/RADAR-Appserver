buildscript {
    ext {
        springBootVersion = '2.1.8.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}
plugins {
    id 'checkstyle'
    id 'pmd'
    id "com.github.spotbugs" version "2.0.0"
    id 'jacoco'
    id "com.github.lkishalmi.gatling" version "3.0.2"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'scala'

group = 'org.radarbase'
version = '0.0.2-SNAPSHOT'
sourceCompatibility = 1.11

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://jitpack.io" }
    maven { url "http://dl.bintray.com/typesafe/maven-releases" }
    maven { url 'https://dl.bintray.com/radar-cns/org.radarcns' }
}

configurations { codacy }

bootJar {
    mainClassName = 'org.radarbase.appserver.AppserverApplication'
}

//apply from: 'gradle/liquibase.gradle'

ext {
    smackVersion = '4.3.1'
    springFoxVersion = '2.9.2'
    springInegrationXmppVersion = '5.1.1.RELEASE'
    lombokVersion = '1.18.4'
    bootAdminVersion = '2.1.1'
    throwingFunctionVersion = '1.5.0'
    mpVersion = '0.5.6'
    junit5Version = "5.4.0"
    jacksonVersion = '2.9.9.1'
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output + test.compileClasspath
            runtimeClasspath += main.output + test.output + test.runtimeClasspath
            srcDir file('src/integrationTest/java')
        }
        resources.srcDir file('src/integrationTest/resources')
    }
}


dependencies {
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')
    implementation('org.springframework.boot:spring-boot-starter-data-rest'){
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
    }
    implementation('org.springframework.boot:spring-boot-starter-web'){
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
    }
    implementation('org.springframework.boot:spring-boot-starter-integration')
    implementation('org.springframework.boot:spring-boot-starter-quartz')
    implementation('org.springframework.boot:spring-boot-starter-security') {
        exclude group: 'org.springframework.security', module: 'spring-security-core'
    }
    implementation('org.springframework.boot:spring-boot-starter-actuator'){
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
    }
    implementation group: 'com.pivovarit', name: 'throwing-function', version: throwingFunctionVersion

    compile (group: 'de.codecentric', name: 'spring-boot-admin-starter-client', version: bootAdminVersion){
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
    }
    implementation (group: 'org.springframework.security.oauth.boot', name: 'spring-security-oauth2-autoconfigure', version: springBootVersion){
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
        exclude group: 'org.springframework.security.oauth', module: 'spring-security-oauth2'
        exclude group: 'org.bouncycastle', module: 'bcprov-jdk15on'
    }
    compile (group: 'org.radarcns', name: 'radar-auth', version: mpVersion){
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
    }

    // Fix potential vulnerabilities
    compile (group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion) {
        force = true
    }
    compile (group: 'org.springframework.security.oauth', name: 'spring-security-oauth2', version: '2.3.6.RELEASE') {
        force = true
        exclude group: 'org.springframework.security', module: 'spring-security-core'
    }
    compile (group: 'org.springframework.security', name: 'spring-security-core', version: '5.1.5.RELEASE') {
        force = true
    }

    // XMPP
    compile group: 'org.springframework.integration', name: 'spring-integration-xmpp', version: springInegrationXmppVersion
    compile (group: 'org.igniterealtime.smack', name: 'smack-experimental', version: smackVersion) {
        exclude group: 'org.bouncycastle', module: 'bcprov-jdk15on'
    }

    compile ('org.bouncycastle:bcprov-jdk15on:1.62') {
        force = true
    }

    // Swagger
    compile (group: 'io.springfox', name: 'springfox-swagger2', version: springFoxVersion) {
        exclude group: 'com.google.guava', module: 'guava'
    }
    compile (group: 'io.springfox', name: 'springfox-swagger-ui', version: springFoxVersion) {
        exclude group: 'com.google.guava', module: 'guava'
    }

    compile ('com.google.guava:guava:28.0-jre') {
        force = true
    }



    compile 'org.scala-lang:scala-library:2.13.1'

    //runtimeOnly('org.springframework.boot:spring-boot-devtools')
    runtimeOnly('org.hsqldb:hsqldb')
    runtimeOnly('org.liquibase:liquibase-core')
    runtime('com.h2database:h2:1.4.199')

    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    compile group: 'org.projectlombok', name: 'lombok', version: lombokVersion

    annotationProcessor "org.springframework:spring-context-indexer:5.1.8.RELEASE"

    compileOnly 'net.jcip:jcip-annotations:1.0'
    compileOnly 'com.github.spotbugs:spotbugs-annotations:3.1.12'

    codacy 'com.github.codacy:codacy-coverage-reporter:-SNAPSHOT'

    testCompile group: 'io.gatling.highcharts', name: 'gatling-charts-highcharts', version: '3.2.1'
    //compile('org.liquibase.ext:liquibase-hibernate5:3.6')
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit', module: 'junit'
    }
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: junit5Version
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junit5Version
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junit5Version
    testRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-commons', version: '1.4.0'
    testRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.4.0'
    testCompileOnly 'com.github.spotbugs:spotbugs-annotations:3.1.3'

    gatling('com.fasterxml.jackson.datatype:jackson-datatype-jsr310')
}

checkstyle {
    configDir file("config/checkstyle")
    toolVersion "8.20"
}

pmd {
    toolVersion "6.14.0"
}

javadoc {
    options.addBooleanOption('html5', true)
    destinationDir = new File("${project.rootDir}/src/main/resources/static/java-docs".toString())
}

wrapper {
    gradleVersion '5.4'
}

test {
    useJUnitPlatform() {
        excludeEngines 'junit-vintage'
    }
}

task unpack(type: Copy) {
    dependsOn bootJar
    from(zipTree(tasks.bootJar.outputs.files.singleFile))
    into("build/dependency")
}

task sendCoverageToCodacy(type: JavaExec, dependsOn: jacocoTestReport) {
    main = "com.codacy.CodacyCoverageReporter"
    classpath = configurations.codacy
    args = [
            "-l",
            "Java",
            "-r",
            "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
    ]
}

task loadTest(type: JavaExec) {
    dependsOn testClasses
    description = "Load Test With Gatling"
    group = "Load Test"
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs = [
            "-Dgatling.core.directory.binaries=${sourceSets.main.output.classesDirs.toString()}"
    ]
    main = "io.gatling.app.Gatling"
    args = [
            "--simulation", "org.radarbase.appserver.ApiGatlingSimulationTest",
            "--results-folder", "${buildDir}/gatling-results",
            "--binaries-folder", sourceSets.main.output.classesDirs.toString(),
            "--bodies-folder", sourceSets.main.resources.srcDirs.toList().first().toString() + "/gatling/bodies",
    ]
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    shouldRunAfter test
}

check.dependsOn integrationTest

integrationTest {
    useJUnitPlatform() {
        excludeEngines 'junit-vintage'
    }
    environment "RADAR_IS_CONFIG_LOCATION", "src/integrationTest/resources/radar_is.yml"
}

