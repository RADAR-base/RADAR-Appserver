buildscript {
    ext {
        springBootVersion = '2.1.0.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}
plugins {
    id 'checkstyle'
    id 'pmd'
    id "com.github.spotbugs" version "1.7.1"
    id 'jacoco'
    id "com.github.lkishalmi.gatling" version "3.0.2"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'scala'

group = 'org.radarbase'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.11

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://jitpack.io" }
    maven { url "http://dl.bintray.com/typesafe/maven-releases" }
}

configurations { codacy }

bootJar {
    mainClassName = 'org.radarbase.appserver.AppserverApplication'
}

//apply from: 'gradle/liquibase.gradle'

ext {
    smackVersion = '4.3.1'
    springFoxVersion = '2.9.2'
    springInegrationXmppVersion = '5.1.1.RELEASE'
    lombokVersion = '1.18.4'
    bootAdminVersion = '2.1.1'
    throwingFunctionVersion = '1.5.0'
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')
    implementation('org.springframework.boot:spring-boot-starter-data-rest')
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-integration')
    implementation('org.springframework.boot:spring-boot-starter-quartz')
    implementation('org.springframework.boot:spring-boot-starter-security')
    implementation('org.springframework.boot:spring-boot-starter-actuator')
    implementation group: 'com.pivovarit', name: 'throwing-function', version: throwingFunctionVersion

    compile group: 'de.codecentric', name: 'spring-boot-admin-starter-client', version: bootAdminVersion

    // XMPP
    compile group: 'org.springframework.integration', name: 'spring-integration-xmpp', version: springInegrationXmppVersion
    compile group: 'org.igniterealtime.smack', name: 'smack-experimental', version: smackVersion

    // Swagger
    compile group: 'io.springfox', name: 'springfox-swagger2', version: springFoxVersion
    compile group: 'io.springfox', name: 'springfox-swagger-ui', version: springFoxVersion

    compile 'org.scala-lang:scala-library:2.12.8'

    //runtimeOnly('org.springframework.boot:spring-boot-devtools')
    runtimeOnly('org.hsqldb:hsqldb')
    runtimeOnly('org.liquibase:liquibase-core')
    runtime('com.h2database:h2')

    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    compile group: 'org.projectlombok', name: 'lombok', version: lombokVersion

    compileOnly 'net.jcip:jcip-annotations:1.0'
    compileOnly 'com.github.spotbugs:spotbugs-annotations:3.1.3'
    //compileOnly("org.springframework:spring-context-indexer")

    codacy 'com.github.codacy:codacy-coverage-reporter:-SNAPSHOT'

    testCompile group: 'io.gatling.highcharts', name: 'gatling-charts-highcharts', version: '2.3.0'
    //compile('org.liquibase.ext:liquibase-hibernate5:3.6')
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit', module: 'junit'
    }
    testImplementation('org.junit.jupiter:junit-jupiter-api')
    testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine')
    testCompileOnly 'com.github.spotbugs:spotbugs-annotations:3.1.3'

    gatling('com.fasterxml.jackson.datatype:jackson-datatype-jsr310')
}

checkstyle {
    configDir file("config/checkstyle")
    toolVersion "8.20"
}

pmd {
    toolVersion "6.14.0"
}

javadoc {
    options.addBooleanOption('html5', true)
    destinationDir = new File("${project.rootDir}/src/main/resources/static/java-docs".toString())
}

wrapper {
    gradleVersion '5.4'
}

test {
    useJUnitPlatform()
}

task unpack(type: Copy) {
    dependsOn bootJar
    from(zipTree(tasks.bootJar.outputs.files.singleFile))
    into("build/dependency")
}

task sendCoverageToCodacy(type: JavaExec, dependsOn: jacocoTestReport) {
    main = "com.codacy.CodacyCoverageReporter"
    classpath = configurations.codacy
    args = [
            "-l",
            "Java",
            "-r",
            "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
    ]
}

task loadTest(type: JavaExec) {
    dependsOn testClasses
    description = "Load Test With Gatling"
    group = "Load Test"
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs = [
            "-Dgatling.core.directory.binaries=${sourceSets.main.output.classesDirs.toString()}"
    ]
    main = "io.gatling.app.Gatling"
    args = [
            "--simulation", "org.radarbase.appserver.ApiGatlingSimulationTest",
            "--results-folder", "${buildDir}/gatling-results",
            "--binaries-folder", sourceSets.main.output.classesDirs.toString(),
            "--bodies-folder", sourceSets.main.resources.srcDirs.toList().first().toString() + "/gatling/bodies",
    ]
}
